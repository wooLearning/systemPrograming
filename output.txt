COPY	START	1000	
FIRST	STL	RETADR		14
CLOOP	JSUB	RDREC		48
	LDA	LENGTH		00
	COMP	ZERO		28
	JEQ	ENDFIL		30
	JSUB	WRREC		48
	J	CLOOP		3c
ENDFIL	LDA	EOF		00
	STA	BUFFER		0c
	LDA 	THREE	
	STA	LENGTH		0c
	JSUB	WRREC		48
	LDL	RETADR		08
	RSUB			4c
EOF 	BYTE	C'EOF'	
THREE	WORD	3	
ZERO	WORD	0	
RETADR	RESW	1	
LENGTH	RESW	1	
BUFFER	RESB	4096	
.			
.	SUBROUTINE TO READ RECORD INTO BUFFER		
.			
RDREC	LDX	ZERO		04
	LDA	ZERO		00
RLOOP	TD	INPUT		e0
	JEQ	RLOOP		30
	RD	INPUT		d8
	COMP	ZERO		28
	JEQ	EXIT		30
	STCH	BUFFER,X		54
	TIX	MAXLEN		2c
	JLT	RLOOP		38
EXIT	STX	LENGTH		10
	RSUB			4c
INPUT	BYTE	X'F1'	
MAXLEN	WORD	4096	
.			
.	SUBROUTINE TO WRITE RECORD FROM BUFFER		
.			
WRREC	LDX	ZERO		04
WLOOP	TD	OUTPUT		e0
	JEQ	WLOOP		30
	LDCH	BUFFER,X		50
	WD	OUTPUT		dc
	TIX	LENGTH		2c
	JLT	WLOOP		38
	RSUB			4c
OUTPUT	BYTE	X'05'	
	END	FIRST	
